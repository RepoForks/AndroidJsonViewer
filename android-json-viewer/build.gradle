group = "com.github.droidpl"
version = "1.0"

apply plugin: 'com.android.library'
apply plugin: "grill"

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

/**
 * Distribution script
 */
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: "documentationRelease") {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

grill {
    documentation {}
    distribute {
        libName "Android JSON Viewer"
        libDescription "Activity to display a json object or array in the UI."
        libDevId "droidpl"
        libDevName "Javier de Pedro LÃ³pez"
        libDevEmail "javierdepedrolopez@gmail.com"
        libSiteUrl "https://github.com/droidpl/AndroidJsonViewer"
        libGitUrl "https://github.com/droidpl/AndroidJsonViewer"
        bintrayRepoName "AndroidJsonViewer"
        bintrayUser System.getenv("DROIDPL_BINTRAY_USER")
        bintrayKey System.getenv("DROIDPL_BINTRAY_KEY")
        bintrayMavenCentralUser System.getenv("DROIDPL_MAVEN_USER")
        bintrayMavenCentralPassword System.getenv("DROIDPL_MAVEN_PASSWORD")
        bintrayArtifactJavadoc project.tasks.getByName("javadocJar")
        bintrayArtifactSources project.tasks.getByName("sourcesJar")
        bintrayMavenSync true
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
}
